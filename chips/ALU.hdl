/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
        Mux16(a= x, b= false, sel= zx, out= xzeroed);
        Not16(in= xzeroed, out= negxzeroed);
        Mux16(a= xzeroed, b= negxzeroed, sel= nx, out= xfinal);
        
        Mux16(a= y, b= false, sel= zy, out= yzeroed);
        Not16(in= yzeroed, out= negyzeroed);
        Mux16(a= yzeroed, b= negyzeroed, sel= ny, out= yfinal);

        Add16(a = xfinal, b = yfinal, out = aAddb);
        And16(a = xfinal, b = yfinal, out = aAndb);
        Mux16(a= aAndb, b= aAddb, sel= f, out= fout);

        Not16(in= fout, out= notfout);
        Mux16(a= fout, b= notfout, sel= no, 
              out=out,
              out[15]=ng,
              out[0..7]=zRL,
              out[8..15]=zRH);

        Or8Way(in= zRL, out= or1);
        Or8Way(in= zRH, out= or2);
        Or(a= or1, b= or2, out= nzr);
        Not(in= nzr, out= zr);
}